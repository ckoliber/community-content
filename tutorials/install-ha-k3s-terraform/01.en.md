---
SPDX-License-Identifier: MIT
path: "/tutorials/install-ha-k3s-terraform"
slug: "install-ha-k3s-terraform"
date: "2022-08-04"
title: "How to install HA K3s Cluster using Terraform"
short_description: "In this tutorial I will show you how to install and configure a Multi-Master Kubernetes Cluster with multiple node pools only using Terraform, installing Traefik Ingress Controller and provisioning Load Balancer"
tags: ["Kubernetes", "K3s", "HA", "Terraform"]
author: "KoLiBer"
author_link: "https://github.com/ckoliber"
author_img: "https://avatars.githubusercontent.com/u/13556168?v=4"
author_description: ""
language: "en"
available_languages: ["en"]
header_img: "header-1"
cta: "cloud"
---

## Introduction

In this tutorial, I will guide on how to setup a Highly Available Kubernetes cluster using K3s and Terraform. [K3s](https://k3s.io/) is a lightweight Kubernetes distribution built for IoT & Edge computing. [Terraform](https://www.terraform.io/) is an infrastructure as code tool using a declarative language called HCL (HashiCorp Configuration Language). In the first section, we will combine these tools in order to make HA K3s cluster. In the last section, we will use Terraform [Helm](https://helm.sh/) provider to deploy [Traefik](https://traefik.io/) IngressController on top of cluster.

**Prerequisites**

- Basic knowledge about the Hetzner Cloud
- Hetzner Cloud API token
- [Terraform](https://www.terraform.io/) is installed and you have basic knowledge about it

## Step 1 - Install Terraform Hetzner Cloud Provider

Create a new folder as the working directory for the next steps, e.g. infrastructure, and create a `main.tf` file with the following content:

```
terraform {
  required_version = ">= 0.14.0"
  required_providers {
    hcloud = {
      source  = "hetznercloud/hcloud"
      version = "~> 1.35.0"
    }
  }
}
```

Now execute `terraform init` to install the [Terraform Hetzner Cloud Provider](https://registry.terraform.io/providers/hetznercloud/hcloud/latest/docs).

## Step 2 - Configure Terraform Hetzner Cloud Provider

Now, we need a Hetzner Cloud API Token, which is used by the terraform provider in order to interract with hetzner cloud, you should export your token as an environment variable:

```
export HCLOUD_TOKEN=<YOUR TOKEN>
```

## Step 3 - Create private network

Now, we'll create a private network which is used by our Kubernetes nodes for communicating with each other. We'll use 192.168.0.0/16 as network and subnet.  
For convenience, we will use [terraform-hcloud-network](https://github.com/cktf/terraform-hcloud-network) module.

Create a `network.tf` file with the following content:

```
module "network" {
  source  = "cktf/network/hcloud"
  version = "1.3.5"

  name = "k3s"
  cidr = "192.168.0.0/16"
  subnets = {
    nodes = {
      type = "cloud",
      cidr = "192.168.0.0/16"
    }
  }
}
```

## Step 4 - Create K3s cluster

It's time. we will use [terraform-hcloud-rke](https://github.com/cktf/terraform-hcloud-rke) module, which is a newly published terraform module, used for bootstrapping highly available kubernetes cluster on top of Hetzner Cloud.
This module will install and configure these manifests on top of cluster:

- [Cloud Controller Manager](https://github.com/hetznercloud/hcloud-cloud-controller-manager)
- [Container Storage Interface](https://github.com/hetznercloud/csi-driver)
- [Cluster Autoscaler](https://github.com/kubernetes/autoscaler/blob/master/cluster-autoscaler/cloudprovider/hetzner/README.md)

So, after creating the cluster, you can easily deploy **Load Balancer** and **Volume** manifests in kubernetes, which will be provisioned on Hetzner Cloud.

Create a `k3s.tf` file with the following content:

```
module "k3s" {
  source  = "cktf/rke/hcloud"
  version = "1.8.3"

  name         = "k3s"
  network_id   = module.network.network_id
  hcloud_token = "<YOUR TOKEN>"

  masters = {
    1 = {
      type     = "cx21"
      location = "fsn1"
    }
    2 = {
      type     = "cx21"
      location = "fsn1"
    }
    3 = {
      type     = "cx21"
      location = "fsn1"
    }
  }

  node_pools = {
    pool1 = {
      type     = "cpx31"
      location = "fsn1"
      min_size = 1
      max_size = 5
    }
    pool2 = {
      type     = "cpx31"
      location = "fsn1"
      min_size = 1
      max_size = 5
    }
  }
}
```

Now execute `terraform apply` to provision the resources.  
Boom!!! your cluster is ready :)

Now, you can see three master servers and one load balancer (used by master nodes) on the hetzner cloud dashboard. after you deploying one application, workers will scale up.

## Step 5 - Deploy Traefik

In this step, we will use terraform `helm` provider in order to deploy `traefik` application on top of our cluster.  
Change the `main.tf` file and add `helm` as a dependency:

```
terraform {
  required_version = ">= 0.14.0"
  required_providers {
    hcloud = {
      source  = "hetznercloud/hcloud"
      version = "~> 1.35.0"
    }
    helm = {
      source  = "hashicorp/helm"
      version = "~> 2.6.0"
    }
  }
}
```

Now, execute `terraform init` to installing helm provider.

### Step 5.1 - Connect to Kubernetes

Next, we must define the cluster credentials for the helm provider.  
Add this provider definition in `main.tf` file:

```
provider "helm" {
  kubernetes {
    host                   = module.k3s.host
    token                  = module.k3s.token
    cluster_ca_certificate = module.k3s.ca_crt
  }
}
```

### Step 5.2 - Deploy Helm application

In the final step, we can deploy traefik using `helm_release` resource in terraform.  
We need to add an annotation to our deployment service in order to provision a load balancer on Hetzner Cloud.

Create a `traefik.tf` file with the following content:

```
resource "helm_release" "traefik" {
  name       = "traefik"
  namespace  = "default"
  repository = "https://helm.traefik.io/traefik"
  chart      = "traefik"
  version    = "10.24.0"

  values = [
    yamlencode({
      service = {
        enabled = true
        annotations = {
          "load-balancer.hetzner.cloud/network-zone"   = "eu-central"
          "load-balancer.hetzner.cloud/use-private-ip" = "true"
        }
      }
    })
  ]
}
```

Now execute `terraform apply` to provision the resources.

## Step 6 - Access traefik

In Hetzner's cloud console, you can see two load balancers, the first one which is named as the cluster name, and used by the master nodes to communicate, and second one which is exposed the traefik ports to the internet, use its IP address and open this url:  
`http://<traefik-ip>`

## Conclusion

You have successfully set up a K3s Kubernetes cluster with three master nodes and two node pools. You're ready to put some workload on your K3s.

##### License: MIT

<!--
Contributor's Certificate of Origin
By making a contribution to this project, I certify that:
(a) The contribution was created in whole or in part by me and I have
    the right to submit it under the license indicated in the file; or
(b) The contribution is based upon previous work that, to the best of my
    knowledge, is covered under an appropriate license and I have the
    right under that license to submit that work with modifications,
    whether created in whole or in part by me, under the same license
    (unless I am permitted to submit under a different license), as
    indicated in the file; or
(c) The contribution was provided directly to me by some other person
    who certified (a), (b) or (c) and I have not modified it.
(d) I understand and agree that this project and the contribution are
    public and that a record of the contribution (including all personal
    information I submit with it, including my sign-off) is maintained
    indefinitely and may be redistributed consistent with this project
    or the license(s) involved.
Signed-off-by: KoLiBer, koliberr136a1@gmail.com
-->
